#!/usr/bin/env perl

# get_strict_ofind_pairs.pl -- Erich Schwarz <ems394@cornell.edu>, 9/11/2020.
# Purpose: given a detailed human-readable description of OrthoFinder groups assigned to a taxon's gene, extract only strict pairs of two designated taxa.
# The input comes from a data column generated by orthomcl_and_phylo/genes2omcls.pl (using argument '-f' to get full OrthoFinder memberships).

use strict;
use warnings;
use autodie;

# Sample input line:
# OG0007394(11 genes,9 taxa):   \
# Acey_s0122.g1098(a_ceylanicum) HCON_00010720(haemonchus) Hbact_v2.g727(heterorhabditis) NECAME_00614(necator) NECAME_00615(necator) KI660311.g615(necator_alt) OTIPU.nOt.2.0.1.g04451(oscheius) OTIPU.nOt.2.0.1.g04453(oscheius) WBGene00111234|PPA21680|Ppa-gcc-1(p_pacificus) Raxei_Eur_2020.08.10.01.g1000(rhabditella) L596_015056(steinernema); \
# OG0008915(14 genes,12 taxa):   \
# Acey_s0562.g3486(a_ceylanicum) Acey_s0562.g3487(a_ceylanicum) CBOVI.g8647(bovis) WBGene00017999|F33D4.7|emc-6(elegans) HCON_00152070(haemonchus) NECAME_13285(necator) Cnig_chr_IV.g13873(nigoni) OTIPU.nOt.2.0.1.g04450(oscheius) exspectatus-mkr-S_50-1.15(p_exspectatus) exspectatus-sn_msk-S_50-1.1(p_exspectatus) WBGene00304395|PPA46616|Ppa-emc-6(p_pacificus) GCK72_014000(remanei) Raxei_Eur_2020.08.10.01.g1000(rhabditella) L596_015055(steinernema)

my $infile = q{};
my $taxon1 = q{};
my $taxon2 = q{};

$infile = $ARGV[0] if $ARGV[0];
$taxon1 = $ARGV[1] if $ARGV[1];
$taxon2 = $ARGV[2] if $ARGV[2];

my $header = "$taxon1\t$taxon2";

if ( (! $infile) or (! $taxon1) or (! $taxon2) ) {
    die	"Format: get_strict_ofind_pairs.pl [infile] [taxon1] [taxon2] >	[ortholog pairs]\n";
}

if ( $taxon1 !~ /\A\w+\z/ ) {
    die "Cannot parse text for taxon 1: $taxon1\n";
}

if ( $taxon2 !~	/\A\w+\z/ ) {
    die	"Cannot	parse text for taxon 2:	$taxon2\n";
}

open my $INFILE, '<', $infile;
while (my $input = <$INFILE>) {
    chomp $input;
    my %taxon1_ortholog = ();
    my %taxon2_ortholog = ();
    while ( $input =~ /(\S+)\($taxon1\)/xmsg ) {
        my $gene = $1;
        $taxon1_ortholog{$gene} = 1;
    }
    while ( $input =~ /(\S+)\($taxon2\)/xmsg ) {
       	my $gene = $1;
       	$taxon2_ortholog{$gene} = 1;
    }
    my @taxon1_genes = sort keys %taxon1_ortholog;
    my @taxon2_genes = sort keys %taxon2_ortholog;

    my $taxon1_gene_count = @taxon1_genes;
    my $taxon2_gene_count = @taxon2_genes;

    if ( ( $taxon1_gene_count == 1 ) and ( $taxon2_gene_count == 1 ) ) {
        # Print header only once, and only if there exist any useful lines:
        print "$header\n" if $header;
        $header = q{};

        # Print strict ortholog pair:
        print "@taxon1_genes\t@taxon2_genes\n";
    }
}
close $INFILE;


